buildscript {
  ext {
    springBootVersion = '3.2.1'
    freefairVersion = '8.6'
  }
}

plugins {
  id 'base'
  id 'io.freefair.lombok' version "${freefairVersion}" // apply, because javadoc
  id  'com.netflix.nebula.project' version '11.0.0' apply false
  id 'com.palantir.git-version' version '3.0.0'
  id 'com.github.spotbugs' version '6.0.5' apply false
  id 'io.freefair.maven-optional' version "${freefairVersion}"
  id 'com.github.jk1.dependency-license-report' version '2.5'
  id 'io.spring.dependency-management' version '1.1.4' apply false

  id 'io.spring.nohttp' version '0.0.11'
  id 'io.freefair.javadoc-links' version "${freefairVersion}"

  id 'org.hibernate.orm' version '6.5.2.Final' apply false
}

def gitVer = gitVersion()

version gitVer.startsWith('v')
  ? gitVer.substring(1, gitVer.endsWith('.dirty') ? gitVer.length() - 6 : gitVer.length())
  : 'edge'

allprojects {
  group 'de.m4rc3l.nova'

  repositories {
    mavenCentral()
  }
}

subprojects {
  version rootProject.version

  apply plugin: 'com.netflix.nebula.project'
  apply plugin: 'io.freefair.maven-optional'
  apply from: "${rootDir}/gradle/java.gradle"
  apply from: "${rootDir}/gradle/lombok.gradle"
  apply from: "${rootDir}/gradle/test.gradle"
  apply from: "${rootDir}/gradle/checkstyle.gradle"
  apply from: "${rootDir}/gradle/javadoc.gradle"
  apply from: "${rootDir}/gradle/publish.gradle"
}
